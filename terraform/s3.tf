data "aws_caller_identity" "current" {}

resource "aws_s3_bucket" "mongodb_backups" {
  bucket        = lower("${var.resource_prefix}-mongodb-backups-${data.aws_caller_identity.current.account_id}")
  force_destroy = true

  tags = {
    Name        = "${var.resource_prefix}MongoBackups"
    Environment = var.resource_prefix
    Project     = "WizExercise"
  }
}

resource "aws_s3_bucket_public_access_block" "mongodb_backups" {
  bucket = aws_s3_bucket.mongodb_backups.id

  block_public_acls       = false
  block_public_policy     = false
  ignore_public_acls      = false
  restrict_public_buckets = false
}

resource "aws_s3_bucket_ownership_controls" "mongodb_backups" {
  bucket = aws_s3_bucket.mongodb_backups.id

  rule {
    object_ownership = "BucketOwnerPreferred"
  }
}

resource "aws_s3_bucket_policy" "mongodb_backups_public" {
  bucket = aws_s3_bucket.mongodb_backups.id

  depends_on = [
    aws_s3_bucket_public_access_block.mongodb_backups
  ]

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Sid       = "PublicListBucket"
        Effect    = "Allow"
        Principal = "*"
        Action    = "s3:ListBucket"
        Resource  = aws_s3_bucket.mongodb_backups.arn
      },
      {
        Sid       = "PublicReadBackupObjects"
        Effect    = "Allow"
        Principal = "*"
        Action    = "s3:GetObject"
        Resource  = "${aws_s3_bucket.mongodb_backups.arn}/*"
      }
    ]
  })
}